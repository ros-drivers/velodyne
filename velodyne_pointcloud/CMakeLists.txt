cmake_minimum_required(VERSION 2.8.3)
project(velodyne_pointcloud)

set(${PROJECT_NAME}_CATKIN_DEPS
    angles
    nodelet
    pcl_ros
    roscpp
    roslib
    sensor_msgs
    tf
    velodyne_driver
    velodyne_msgs)

find_package(catkin REQUIRED COMPONENTS
             ${${PROJECT_NAME}_CATKIN_DEPS} pcl_conversions roslaunch rostest)
find_package(Boost COMPONENTS signals)
find_package(pcl)

# Resolve system dependency on yaml-cpp, which apparently does not
# provide a CMake find_package() module.
find_package(PkgConfig REQUIRED)
pkg_check_modules(YAML_CPP REQUIRED yaml-cpp)
find_path(YAML_CPP_INCLUDE_DIR
          NAMES yaml_cpp.h
          PATHS ${YAML_CPP_INCLUDE_DIRS})
find_library(YAML_CPP_LIBRARY
             NAMES YAML_CPP
             PATHS ${YAML_CPP_LIBRARY_DIRS})

include_directories(include ${catkin_INCLUDE_DIRS})

catkin_package(
    CATKIN_DEPENDS ${${PROJECT_NAME}_CATKIN_DEPS}
    DEPENDS yaml-cpp python-yaml
    INCLUDE_DIRS include
    LIBRARIES velodyne_rawdata)

add_subdirectory(src/lib)
add_subdirectory(src/conversions)

install(DIRECTORY include/${PROJECT_NAME}/
        DESTINATION ${CATKIN_PACKAGE_INCLUDE_DESTINATION})
install(FILES nodelets.xml
        DESTINATION ${CATKIN_PACKAGE_SHARE_DESTINATION})
install(DIRECTORY launch/
        DESTINATION ${CATKIN_PACKAGE_SHARE_DESTINATION}/launch)
install(DIRECTORY params/
        DESTINATION ${CATKIN_PACKAGE_SHARE_DESTINATION}/params)

if (CATKIN_ENABLE_TESTING)
  message("-- ${PROJECT_NAME} unit testing enabled")

  # Download packet capture (PCAP) files containing test data.
  # Store them in devel-space, so rostest can easily find them.
  catkin_download_test_data(
    ${PROJECT_NAME}_tests_class.pcap
    http://pr.willowgarage.com/data/velodyne/class.pcap
    DESTINATION ${CATKIN_DEVEL_PREFIX}/${CATKIN_PACKAGE_SHARE_DESTINATION}/tests
    MD5 65808d25772101358a3719b451b3d015)
  catkin_download_test_data(
    ${PROJECT_NAME}_tests_32e.pcap
    http://pr.willowgarage.com/data/velodyne/32e.pcap
    DESTINATION ${CATKIN_DEVEL_PREFIX}/${CATKIN_PACKAGE_SHARE_DESTINATION}/tests
    MD5 e41d02aac34f0967c03a5597e1d554a9)
  
if (USE_64E_S2.1)
  catkin_download_test_data(
    ${PROJECT_NAME}_tests_64e_s2.1-300-sztaki.pcap
    https://dl.dropboxusercontent.com/u/107064156/64e_s2.1-300-sztaki.pcap
    DESTINATION ${CATKIN_DEVEL_PREFIX}/${CATKIN_PACKAGE_SHARE_DESTINATION}/tests
    MD5 176c900ffb698f9b948a13e281ffc1a2)
endif (USE_64E_S2.1)  

  # unit tests
  add_rostest(tests/cloud_node_hz.test)
  add_rostest(tests/cloud_nodelet_hz.test)
  add_rostest(tests/cloud_node_32e_hz.test)
  add_rostest(tests/cloud_nodelet_32e_hz.test)

if (USE_64E_S2.1)
  add_rostest(tests/cloud_node_64e_s2.1_hz.test)
  add_rostest(tests/cloud_nodelet_64e_s2.1_hz.test)
endif (USE_64E_S2.1)  

  ## These tests don't work well enough to be worth the effort of
  ## running them:
  #add_rostest(tests/transform_node_hz.test)
  #add_rostest(tests/transform_nodelet_hz.test)
  
  # parse check all the launch/*.launch files
  ##roslaunch_add_file_check(launch)

else ()
  message("-- ${PROJECT_NAME} unit testing disabled")
endif (CATKIN_ENABLE_TESTING)
